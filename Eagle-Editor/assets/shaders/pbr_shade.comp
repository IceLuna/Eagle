#include "pbr_pipeline_layout.h"
#include "utils.h"
#include "pbr_utils.h"
#include "shadows_utils.h"

#define EG_BASE_REFLECTIVITY 0.04f

#extension GL_EXT_nonuniform_qualifier : enable

layout(set = 2, binding = 0, rgba32f) uniform writeonly image2D g_Result;

layout(push_constant) uniform PushConstants
{
    mat4 g_ViewProjInv;
    vec3 g_CameraPos;
    uint g_PointLightsCount;
    ivec2 g_Size;
    uint g_SpotLightsCount;
    uint g_HasDirectionalLight;
    float g_MaxReflectionLOD;
    uint g_HasIrradiance;
};

#define GROUP_SIZE 8
layout(local_size_x = GROUP_SIZE, local_size_y = GROUP_SIZE) in;

void main()
{
    const ivec2 pixelCoords = ivec2(gl_GlobalInvocationID);
    if (pixelCoords.x >= g_Size.x || pixelCoords.y >= g_Size.y)
        return;

    const vec2 texelSize = 1.f / g_Size;
    const vec2 uv = (pixelCoords + 0.5) * texelSize;

    const vec4 encodedNormals = texture(g_GeometryShadingNormalsTexture, uv); // xy - geometry, zw - shading

    const vec4 albedo_roughness = texture(g_AlbedoTexture, uv);
    const vec3 lambert_albedo = albedo_roughness.rgb * EG_INV_PI;
    const float depth = texture(g_DepthTexture, uv).x;
    const vec3 worldPos = WorldPosFromDepth(g_ViewProjInv, uv, depth);
    const vec3 geometryNormal = DecodeNormal(encodedNormals.xy);
    const vec3 shadingNormal = DecodeNormal(encodedNormals.zw);
    const vec3 emissive = texture(g_EmissiveTexture, uv).xyz;
    const vec2 materialData = texture(g_MaterialDataTexture, uv).xy;
 
#ifdef EG_SSAO
    const float ssao = texture(g_SSAO, uv).x;
#endif

    const float metallness = materialData.x;
    const float ao = materialData.y;
    const float roughness = albedo_roughness.a;
    vec3 F0 = vec3(EG_BASE_REFLECTIVITY);
    F0 = mix(F0, albedo_roughness.rgb, metallness);
    const vec3 V = normalize(g_CameraPos - worldPos);

    vec3 Lo = vec3(0.f);

    // PointLights
    uint plShadowMapIndex = 0;
    for (uint i = 0; i < g_PointLightsCount; ++i)
    {
        const PointLight pointLight = g_PointLights[i];
        const vec3 incoming = pointLight.Position - worldPos;
	    const float distance2 = dot(incoming, incoming);
        const bool bCastsShadows = (floatBitsToUint(pointLight.Intensity) & 0x80000000) != 0;
        if (distance2 > (pointLight.Radius * pointLight.Radius))
        {
            if (bCastsShadows)
                plShadowMapIndex++;
            continue;
        }
	    const float attenuation = 1.f / distance2;

        const vec3 normIncoming = normalize(incoming);
        const vec3 pointLightLo = EvaluatePBR(lambert_albedo, normIncoming, V, shadingNormal, F0, metallness, roughness, pointLight.LightColor, abs(pointLight.Intensity) * attenuation);
        float shadow = 1.f;
        if (bCastsShadows)
        {
            const float NdotL = clamp(dot(normIncoming, geometryNormal), EG_FLT_SMALL, 1.0);
            shadow = plShadowMapIndex < EG_MAX_LIGHT_SHADOW_MAPS ? PointLight_ShadowCalculation(g_PointShadowMaps[plShadowMapIndex], -incoming, NdotL) : 1.f;
            plShadowMapIndex++;
        }
        Lo += pointLightLo * shadow;
    }

    // SpotLights
    uint slShadowMapIndex = 0;
    for (uint i = 0; i < g_SpotLightsCount; ++i)
    {
        const SpotLight spotLight = g_SpotLights[i];
        const vec3 incoming = spotLight.Position - worldPos;
	    const float distance2 = dot(incoming, incoming);
        if (distance2 > (spotLight.Distance * spotLight.Distance))
        {
            if (spotLight.bCastsShadows != 0)
                slShadowMapIndex++;
            continue;
        }

	    const float attenuation = 1.f / distance2;
        const vec3 normIncoming = normalize(incoming);

        //Cutoff
	    const float innerCutOffCos = cos(spotLight.InnerCutOffRadians);
	    const float outerCutOffCos = cos(spotLight.OuterCutOffRadians);
	    const float epsilon = innerCutOffCos - outerCutOffCos;
        const float theta = clamp(dot(normIncoming, normalize(-spotLight.Direction)), EG_FLT_SMALL, 1.0);
	    const float cutoffIntensity = clamp((theta - outerCutOffCos) / epsilon, 0.0, 1.0);

        vec4 lightSpacePos = spotLight.ViewProj * vec4(worldPos, 1.0);
        lightSpacePos.xyz /= lightSpacePos.w;

        float shadow = 1.f;
        if (spotLight.bCastsShadows != 0)
        {
            const float NdotL = clamp(dot(normIncoming, geometryNormal), EG_FLT_SMALL, 1.0);
            shadow = slShadowMapIndex < EG_MAX_LIGHT_SHADOW_MAPS ? SpotLight_ShadowCalculation(g_SpotShadowMaps[slShadowMapIndex], lightSpacePos.xyz, NdotL) : 1.f;
            slShadowMapIndex++;
        }
        const vec3 spotLightLo = EvaluatePBR(lambert_albedo, normIncoming, V, shadingNormal, F0, metallness, roughness, spotLight.LightColor, spotLight.Intensity * cutoffIntensity * attenuation);
        Lo += spotLightLo * shadow;
    }

    // Directional light
#ifdef EG_ENABLE_CSM_VISUALIZATION
    vec3 cascadeVisualizationColor = vec3(1.f);
#endif
    if (g_HasDirectionalLight > 0)
    {
        const float cascadeDepth = abs((g_CameraView * vec4(worldPos, 1.0)).z);
        int layer = -1;
        for (int i = 0; i < EG_CASCADES_COUNT; ++i)
        {
            if (cascadeDepth < g_DirectionalLight.CascadePlaneDistances[i])
            {
                layer = i;
                break;
            }
        }

        const vec3 incoming = normalize(-g_DirectionalLight.Direction);
        vec3 directional_Lo = EvaluatePBR(lambert_albedo, incoming, V, shadingNormal, F0, metallness, roughness, g_DirectionalLight.LightColor, g_DirectionalLight.Intensity);
        float shadow = 1.f;
        if (layer != -1)
        {
#ifdef EG_ENABLE_CSM_VISUALIZATION
            const vec3 cascadeColors[EG_CASCADES_COUNT] = vec3[]
            (
                vec3(1, 0, 0),
                vec3(0, 1, 0),
                vec3(0, 0, 1),
                vec3(1, 0, 1)
            );
            cascadeVisualizationColor = cascadeColors[layer];
#endif
            const float NdotL = clamp(dot(incoming, geometryNormal), EG_FLT_SMALL, 1.0);
            const vec3 lightSpacePos = (g_DirectionalLight.ViewProj[layer] * vec4(worldPos, 1.0)).xyz;
            if (g_DirectionalLight.bCastsShadows != 0)
                shadow = DirLight_ShadowCalculation(g_DirShadowMaps[nonuniformEXT(layer)], lightSpacePos, NdotL);
        }
        Lo += directional_Lo * shadow;
    }

    // Ambient
    vec3 ambient = vec3(0.f);
    if (g_HasIrradiance > 0)
    {
        const vec3 R = reflect(-V, shadingNormal);
        const float NdotV = clamp(dot(shadingNormal, V), EG_FLT_SMALL, 1.0);

        const vec3 Fr = max(vec3(1.f - roughness), F0) - F0;
        const vec3 kS = F0 + Fr * pow(1.f - NdotV, 5.f);

        const vec2 envBRDF = texture(g_BRDFLUT, vec2(NdotV, roughness)).rg;
        const vec3 FssEss = kS * envBRDF.x + envBRDF.y;

        // Multiple scattering, from Fdez-Aguera
        const float Ems = (1.0 - (envBRDF.x + envBRDF.y));
        const vec3 Favg = F0 + (1.0 - F0) / 21.0;
        const vec3 FmsEms = Ems * FssEss * Favg / (1.0 - Favg * Ems);

        const vec3 diffuseColor = albedo_roughness.rgb * (1.f - EG_BASE_REFLECTIVITY) * (1.f - metallness);
        const vec3 kD = diffuseColor * (1.0 - FssEss - FmsEms);

        const vec3 radiance = textureLod(g_PrefilterMap, R, roughness * g_MaxReflectionLOD).rgb;
        const vec3 irradiance = texture(g_IrradianceMap, shadingNormal).rgb;
        const vec3 color = FssEss * radiance + (FmsEms + kD) * irradiance;
        ambient = color * ao;
#ifdef EG_SSAO
        ambient *= ssao;
#endif
    }

    vec3 resultColor = ambient + Lo + emissive;
#ifdef EG_ENABLE_CSM_VISUALIZATION
    resultColor += cascadeVisualizationColor;
#endif

    imageStore(g_Result, pixelCoords, vec4(resultColor, 1.f));
}
